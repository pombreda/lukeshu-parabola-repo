# Maintainer: Luke Shumaker <lukeshu@sbcglobal.net>
# Contributor: max-k <max-k@post.com>

pkgname=gns3
pkgver=0.7.4
pkgrel=3
pkgdesc="Graphical network simulator that allows simulation of complex networks."
arch=(i686 x86_64)
url="http://www.gns3.net/"
license=('GPL')
depends=('python2-pyqt' 'python2-sip' 'inetutils')
optdepends=('wireshark' 'dynagen' 'qemu-mcast')
backup=('etc/gns3/gns3.ini' 'etc/gns3/baseconfig.txt')
source=("http://downloads.sourceforge.net/gns-3/GNS3-${pkgver}-src.tar.gz" 'gns3.ini')

build() {
  cd "${srcdir}/GNS3-${pkgver}-src"
  python2 setup.py build
  sed -i 's@#!/usr/bin/env python@#!/usr/bin/env python2@' ./qemuwrapper/qemuwrapper.py
}

package() {
  cd "${srcdir}/GNS3-${pkgver}-src"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m 644 ./docs/man/gns3.1 "${pkgdir}/usr/share/man/man1/gns3.1"
  install -d "${pkgdir}/usr/share/GNS3"
  install -m 755 ./qemuwrapper/{pemubin,qemuwrapper}.py "${pkgdir}/usr/share/GNS3"

  install -d "${pkgdir}/etc/gns3"
  install -m 644 "${srcdir}/gns3.ini" baseconfig.txt "${pkgdir}/etc/gns3"
}

md5sums=('777ae9d65382e0428952cba21d5c0309'
         '9028399d58d71ef47f5282b51cc42e38')
