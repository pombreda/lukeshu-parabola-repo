# Contributor: Marcel Wysocki <maci@satgnu.net>
# Maintainer: Marcel Wysocki <maci@satgnu.net>

pkgname=dynagen
pkgver=0.11.0
pkgrel=2
pkgdesc="Dynagen is a front-end for use with the Dynamips Cisco router emulator."
arch=('i686' 'x86_64')
url="http://www.dynagen.org"
license=('GPL2')
depends=('dynamips' 'python2')
backup=('etc/dynagen.ini')
source=(http://downloads.sourceforge.net/dyna-gen/${pkgname}-${pkgver}.tar.gz)

# TODO: Figure out PEMU, there's a binary in the distro. Remove pemubin.py

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rm pemubin.py # is nonfree

  chmod 644 dynagen
  mv dynagen dynagen.py
  sed -i '1s@^#!.*@#!/usr/bin/env python2@' *.py
  echo '#!/bin/sh' > dynagen.sh
  echo 'export PYTHONPATH=/usr/share/dynagen' >> dynagen.sh
  echo 'python2 /usr/share/dynagen/dynagen.py' >> dynagen.sh
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d "${pkgdir}/usr/share/doc/"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 README.txt "${pkgdir}/usr/share/doc/"
  
  install -d "${pkgdir}/usr/share/dynagen"
  install -m644 *.py configspec "${pkgdir}/usr/share/dynagen"
  install -m755 pemu-start.sh "${pkgdir}/usr/share/dynagen"
  
  install -D -m755 dynagen.sh "${pkgdir}/usr/bin/dynagen"
  install -D -m644 dynagen.ini "${pkgdir}/etc/dynagen.ini"
}

md5sums=('3f88b3449b17096dca84d007f0b91b3f')
