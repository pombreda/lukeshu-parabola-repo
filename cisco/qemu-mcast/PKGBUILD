# Maintainer: Marek Skrobacki <skrobul@skrobul.com>

pkgname=qemu-mcast
pkgver=0.13.0
pkgrel=1
pkgdesc="A generic and open source processor emulator which achieves a good emulation speed by using dynamic translation. This version has been patched to provide multicast functionality (used for GNS3)."
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
url="http://wiki.qemu.org/Index.html"
makedepends=('texi2html' 'perl')
depends=('libjpeg' 'libpng' 'sdl' 'alsa-lib' 'nss' 'glib2' 'gnutls>=2.4.1' 'bluez' 'vde2' 'util-linux-ng' 'curl' 'libsasl')
conflicts=('qemu')
provides=('qemu')
backup=('etc/qemu/target-x86_64.conf')
install=qemu.install
source=(http://savannah.nongnu.org/download/qemu/qemu-${pkgver}.tar.gz
        http://downloads.sourceforge.net/project/gns-3/Qemu/qemu-0.13.0-patches.zip
        65-kvm.rules)
options=(!strip)

build()
{
  cd ${srcdir}/qemu-${pkgver}
  patch -p1 -i ${srcdir}/qemu-${pkgver}-mcast-udp.patch || return 1
  sed -i -e 's/lib64/lib/g' x86_64.ld
  ./configure --prefix=/usr --sysconfdir=/etc --audio-drv-list=oss,alsa,sdl \
              --audio-card-list=ac97,sb16,es1370,hda \
              --enable-docs
  make
}

package() {
  cd ${srcdir}/qemu-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/65-kvm.rules \
                   ${pkgdir}/lib/udev/rules.d/65-kvm.rules
}

md5sums=('397a0d665da8ba9d3b9583629f3d6421'
         '60664b8839e3e24710bb4e0ae87152fa'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf')
