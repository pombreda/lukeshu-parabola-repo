# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Maintainer: Luke Shumaker <lukeshu@sbcglobal.net>

pkgname=(
  'unarchiver'
  'unar'
  'libuniversaldetector'
  'libxadmaster'
  'libxadmaster-libxad')
pkgver=2.7.1
pkgrel=4
arch=('x86_64' 'i686')
url="http://wakaba.c3.cx/s/apps/unarchiver.html"
license=('LGPL2.1')
depends=('gnustep-base-libre>=1.23.0' 'openssl' 'bzip2' 'icu' 'gcc-libs' 'zlib')
conflicts=('gnustep-base>=1.24')
makedepends=('gcc-objc')
source=("http://theunarchiver.googlecode.com/files/TheUnarchiver${pkgver}_src.zip")

build() {
  cd "$srcdir/The Unarchiver/XADMaster"

  # build everything
  . /usr/share/GNUstep/Makefiles/GNUstep.sh
  make -f Makefile.linux

  # make libxad into a lib file
  cd libxad
  rm -f libxad.a
  ar rcs libxad.a all.o clients.o unix/emulation.o unix/init.o
}

package_unarchiver() {
  pkgdesc="An Objective-C application for uncompressing archive files"
  arch=('any')
  depends=(${depends[@]} 'unar>=0.4')
}

package_unar() {
  pkgver=0.4
  pkgdesc="The unar/lsar command line wrappers around libXADMaster"

  cd "$srcdir/The Unarchiver/XADMaster"
  install -d "$pkgdir/usr/bin"
  install -m755 unar lsar "$pkgdir/usr/bin"
}

package_libuniversaldetector() {
  pkgver=0.4
  pkgdesc="Some fancy-schmancy Objective-C library that detects things?"

  cd "$srcdir/The Unarchiver/UniversalDetector"
  install -d "$pkgdir/usr/lib"
  install libUniversalDetector.a "$pkgdir/usr/lib"
  install -d "$pkgdir/usr/include"
  install UniversalDetector.h "$pkgdir/usr/include"
}

package_libxadmaster() {
  pkgver=0.4
  pkgdesc="An Objective-C library built around libxad adding support for other formats"
  depends+=('libuniversaldetector')

  header_files='
    CommandLineCommon.h
    NSStringPrinting.h
    XADArchive.h
    XADRegex.h
    XADUnarchiver.h
    '

  cd "$srcdir/The Unarchiver/XADMaster"
  install -d "$pkgdir/usr/lib"
  install libXADMaster.a "$pkgdir/usr/lib"
  install -d "$pkgdir/usr/include/XADMaster"
  install $header_files "$pkgdir/usr/include/XADMaster"
}

package_libxadmaster-libxad() {
  pkgver=0.4 # forked from 13.0.2005.06.23 (commit 1.24)
  pkgdesc="A fork of libxad, the port of the Amiga xadmaster.library to *NIX"
  depends=('glibc')
  provides=('libxad')
  conflicts=('libxad')

  cd "$srcdir/The Unarchiver/XADMaster/libxad"
  install -d "$pkgdir/usr/lib"
  install libxad.a "$pkgdir/usr/lib"
  install -d "$pkgdir/usr/include/libxad"
  install include/* "$pkgdir/usr/include/libxad"
}

md5sums=('498ea1c984d5783322e070a71922b422')
