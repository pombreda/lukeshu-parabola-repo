# Maintainer: Max Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=dasm
pkgver=2.20.11
pkgrel=3
pkgdesc="An assembler for the 6502, 68HC11, 68705, 6803, HD6303, F8 and 6507 microprocessors"
arch=('i686' 'x86_64')
url="http://www.atari2600.org/DASM"
license=('GPL')
source=("http://downloads.sourceforge.net/project/$pkgname-dillon/$pkgname-dillon/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('3e67f7b8ac80419d53cc0aaa9a47ab37')

build() {
 cd "$srcdir/$pkgname-$pkgver"
 make
}

package() {
 install -Dm 755 "$srcdir/$pkgname-$pkgver/bin/dasm" "$pkgdir/usr/bin/dasm"
 install -Dm 755 "$srcdir/$pkgname-$pkgver/bin/ftohex" "$pkgdir/usr/bin/ftohex"

 for doc in AUTHORS ChangeLog COPYING CREDITS FUTURE NEWS README README.ANDREW "doc/dasm.txt" "doc/ftohex.txt"; do
  install -Dm 644 "$srcdir/$pkgname-$pkgver/$doc" "$pkgdir/usr/share/doc/dasm/${doc/*\/}"
 done

 mkdir -p "$pkgdir/usr/share/dasm"
 cp -R "$srcdir/$pkgname-$pkgver"/{machines,test} "$pkgdir/usr/share/dasm"

 chmod 755 "$pkgdir/usr/share"/{dasm,dasm/machines,dasm/machines/atari2600,dasm/machines/channel-f,dasm/test}
 chmod 644 "$pkgdir/usr/share/dasm"/{machines/atari2600,machines/channel-f,test}/*
}
